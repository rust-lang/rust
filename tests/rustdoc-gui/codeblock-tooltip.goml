// Checking the colors of the codeblocks tooltips.
goto: "file://" + |DOC_PATH| + "/test_docs/fn.foo.html"
show-text: true

define-function: (
    "check-colors",
    (theme, background, color, border),
    block {
        // Setting the theme.
        local-storage: {"rustdoc-theme": |theme|, "rustdoc-use-system-theme": "false"}
        reload:

        // compile_fail block
        assert-css: (
            ".docblock .example-wrap.compile_fail .tooltip",
            {"color": "rgba(255, 0, 0, 0.5)"},
        )
        assert-css: (
            ".docblock .example-wrap.compile_fail",
            {"border-left": "2px solid rgba(255, 0, 0, 0.5)"},
        )

        move-cursor-to: ".docblock .example-wrap.compile_fail .tooltip"

        assert-css: (
            ".docblock .example-wrap.compile_fail .tooltip",
            {"color": "rgb(255, 0, 0)"},
        )
        assert-css: (
            ".docblock .example-wrap.compile_fail",
            {"border-left": "2px solid rgb(255, 0, 0)"},
        )
        click: ".docblock .example-wrap.compile_fail .tooltip"
        assert-text: (
            ".popover.tooltip",
            "This example deliberately fails to compile"
        )
        assert-css: (".popover.tooltip", {
            "color": |color|,
            "background-color": |background|,
            "border-color": |border|,
        })

        // should_panic block
        assert-css: (
            ".docblock .example-wrap.should_panic .tooltip",
            {"color": "rgba(255, 0, 0, 0.5)"},
        )
        assert-css: (
            ".docblock .example-wrap.should_panic",
            {"border-left": "2px solid rgba(255, 0, 0, 0.5)"},
        )

        move-cursor-to: ".docblock .example-wrap.should_panic .tooltip"

        assert-css: (
            ".docblock .example-wrap.should_panic .tooltip",
            {"color": "rgb(255, 0, 0)"},
        )
        assert-css: (
            ".docblock .example-wrap.should_panic",
            {"border-left": "2px solid rgb(255, 0, 0)"},
        )
        click: ".docblock .example-wrap.should_panic .tooltip"
        assert-text: (
            ".popover.tooltip",
            "This example panics"
        )
        assert-css: (".popover.tooltip", {
            "color": |color|,
            "background-color": |background|,
            "border-color": |border|,
        })

        // ignore block
        assert-css: (
            ".docblock .example-wrap.ignore .tooltip",
            {"color": "rgba(255, 142, 0, 0.6)"},
        )
        assert-css: (
            ".docblock .example-wrap.ignore",
            {"border-left": "2px solid rgba(255, 142, 0, 0.6)"},
        )

        move-cursor-to: ".docblock .example-wrap.ignore .tooltip"

        assert-css: (
            ".docblock .example-wrap.ignore .tooltip",
            {"color": "rgb(255, 142, 0)"},
        )
        assert-css: (
            ".docblock .example-wrap.ignore",
            {"border-left": "2px solid rgb(255, 142, 0)"},
        )
        click: ".docblock .example-wrap.ignore .tooltip"
        assert-text: (
            ".popover.tooltip",
            "This example is not tested"
        )
        assert-css: (".popover.tooltip", {
            "color": |color|,
            "background-color": |background|,
            "border-color": |border|,
        })
        click: ".docblock .example-wrap.ignore .tooltip"
        assert-false: ".popover.tooltip"
    },
)

call-function: ("check-colors", {
    "theme": "ayu",
    "background": "rgb(15, 20, 25)",
    "color": "rgb(197, 197, 197)",
    "border": "rgb(92, 103, 115)",
})
call-function: ("check-colors", {
    "theme": "dark",
    "background": "rgb(53, 53, 53)",
    "color": "rgb(221, 221, 221)",
    "border": "rgb(224, 224, 224)",
})
call-function: ("check-colors", {
    "theme": "light",
    "background": "rgb(255, 255, 255)",
    "color": "rgb(0, 0, 0)",
    "border": "rgb(224, 224, 224)",
})
