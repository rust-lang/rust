// Ensures that the theme is working when going back in history.
go-to: "file://" + |DOC_PATH| + "/test_docs/index.html"
// Set the theme to dark.
set-local-storage: {
    "rustdoc-theme": "dark",
    "rustdoc-use-system-theme": "false",
}
// We reload the page so the local storage settings are being used.
reload:
assert-css: ("body", { "background-color": "#353535" })
assert-local-storage: { "rustdoc-theme": "dark" }

// Now we go to the settings page.
go-to: "file://" + |DOC_PATH| + "/settings.html"
wait-for: "#settings"
// We change the theme to "light".
click: "#theme-light"
wait-for-css: ("body", { "background-color": "white" })
assert-local-storage: { "rustdoc-theme": "light" }

// We go back in history.
history-go-back:
// Confirm that we're not on the settings page.
assert-false: "#settings"
// Check that the current theme is still "light".
assert-css: ("body", { "background-color": "white" })
assert-local-storage: { "rustdoc-theme": "light" }
